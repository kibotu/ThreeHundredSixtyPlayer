ext {
    versionName = VERSION_NAME
    VERSION_CODE = 1
}
apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

android {

    namespace "com.exozet.threehundredsixtyplayer"
    compileSdk compileSdkVer
    buildToolsVersion = buildToolsVer

    defaultConfig {
        minSdk minSdkVer
        targetSdk targetSdkVer
        versionCode VERSION_CODE
        versionName VERSION_NAME

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'

        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        viewBinding true
    }

    // region java compile options

    java {
        toolchain {
            languageVersion.set(JavaLanguageVersion.of(17))
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    // endregion

    // region kotlin compile options

    kotlinOptions {
        jvmTarget = "17"
        allWarningsAsErrors = false
        kotlinOptions.verbose = true

        //## Hint: https://kotlinlang.org/docs/reference/java-to-kotlin-interop.html#default-methods-in-interfaces
        //## Hint: "-Xjvm-default=all-compatibility" and "-Xjvm-default=all-compatibility" will also work.
        freeCompilerArgs += [
                "-Xjvm-default=all-compatibility",
                // disable linting for opt-in annotations
                "-opt-in=kotlin.RequiresOptIn"
        ]
    }

    // endregion

    // region lint options

    lint {
        disable 'TypographyFractions', 'TypographyQuotes', 'InvalidPackage'
        enable 'RtlHardcoded', 'RtlCompat', 'RtlEnabled'
        checkOnly 'NewApi', 'InlinedApi'
        quiet true
        abortOnError false
        ignoreWarnings true
        checkDependencies true
        ignoreTestSources true
        checkAllWarnings false
    }

    // endregion

    // region exclude duplicated meta inf files

    packagingOptions {
        // DebugProbesKt.bin is used for java debugging (not needed for android)
        // Hint: https://github.com/Kotlin/kotlinx.coroutines/issues/2274
        exclude 'DebugProbesKt.bin'

        excludes += '/META-INF/{AL2.0,LGPL2.1}'
        // https://stackoverflow.com/a/61893957/1006741
        excludes -= "/META-INF/*.kotlin_module"
    }

    // endregion
}

dependencies {
    coreLibraryDesugaring desugaring.jdk
    implementation libs.supportAppCompat
    implementation libs.constraintLayout

    implementation libs.rx2java
    implementation libs.rx2Android
    implementation libs.glide
    implementation libs.glideOkHttp3
    implementation libs.md360Player
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = "net.kibotu"
                artifactId = 'ThreeHundertSixtyPlayer'
                version = VERSION_NAME
            }
        }
    }
}
